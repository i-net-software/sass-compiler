plugins {
  id 'maven-publish'
  id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
  id 'signing'
  id 'java'
}

group = 'de.inetsoftware'
archivesBaseName = 'sass-compiler'
version = '1.3'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jsoup:jsoup:1.6.3'
    testImplementation 'commons-io:commons-io:2.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://ossrh-staging-api.central.sonatype.com/service/local/')
            snapshotRepositoryUrl = uri('https://central.sonatype.com/repository/maven-snapshots/')
            username = project.findProperty('ossrhUsername')
            password = project.findProperty('ossrhPassword')
        }
    }
}
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Java Sass Compiler'
                description = 'A Sass compiler written completely in Java (pure Java).'
                url = 'https://github.com/i-net-software/sass-compiler'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Horcrux7'
                        name = 'Volker Berlin'
                        email = 'vberlin@inetsoftware.de'
                        organization = 'i-net software'
                        organizationUrl = 'https://www.inetsoftware.de'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:i-net-software/sass-compiler.git'
                    developerConnection = 'scm:git:git@github.com:i-net-software/sass-compiler.git'
                    url = 'https://github.com/i-net-software/sass-compiler'
                }
            }
        }
    }
}

signing {
    required { project.hasProperty('signing.keyId') || project.hasProperty('signing.key') }
    if( project.hasProperty('signing.key') ) {
        useInMemoryPgpKeys( project.findProperty('signing.key'), project.findProperty('signing.password') )
    }
    sign publishing.publications.maven
}

closeStagingRepositories.dependsOn 'publishToSonatype'
publish.dependsOn 'closeStagingRepositories'
